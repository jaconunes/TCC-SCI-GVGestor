unit uPadraoRelatorioGVGESTOR;

interface

uses
  Winapi.Windows, Winapi.Messages, System.SysUtils, System.Variants, System.Classes, Vcl.Graphics,
  Vcl.Controls, Vcl.Forms, Vcl.Dialogs, Vcl.StdCtrls, Vcl.ToolWin, Vcl.ComCtrls,
  System.ImageList, Vcl.ImgList, Data.FMTBcd, frxExportDOCX, frxClass,
  frxExportPDF, Data.DB, Data.SqlExpr, frxDBSet, frxDesgn, udmConnectionGVGestor,
  Vcl.ExtCtrls, Datasnap.DBClient;

type
  TfrPadraoRelatorioGVGESTOR = class(TForm)
    ToolBar1: TToolBar;
    GroupBox1: TGroupBox;
    btVisualizar: TToolButton;
    btSalvarComo: TToolButton;
    btImprimir: TToolButton;
    ImageList1: TImageList;
    frxReportPadrao: TfrxReport;
    frxDesigner1: TfrxDesigner;
    frxDBDataset1: TfrxDBDataset;
    SQLQueryPadrao: TSQLQuery;
    PrintDialog1: TPrintDialog;
    frxPDFExport1: TfrxPDFExport;
    frxDOCXExport1: TfrxDOCXExport;
    SaveDialog1: TSaveDialog;
    btEditar: TToolButton;
    ToolButton2: TToolButton;
    ToolButton3: TToolButton;
    ToolButton4: TToolButton;
    ToolButton5: TToolButton;
    Panel1: TPanel;
    procedure FormCreate(Sender: TObject);
    procedure btSalvarComoClick(Sender: TObject);
    procedure FormClose(Sender: TObject; var Action: TCloseAction);
    procedure btImprimirClick(Sender: TObject);
    procedure btEditarClick(Sender: TObject);
    procedure btVisualizarClick(Sender: TObject);
    procedure FormKeyDown(Sender: TObject; var Key: Word; Shift: TShiftState);
    procedure FormResize(Sender: TObject);
  private
    { Private declarations }

  public
    { Public declarations }
     procedure pGetConsultaSql; virtual; abstract;
     procedure fCarregaFrxPadrao(wName: String); virtual;
     procedure pGetConsultaMasterSource; virtual; abstract;
     function fGetNomeArquivo: String; virtual; abstract;
     procedure setLimpaFiltros(wTabela: TClientDataset); virtual;
     procedure setResetMasterSource(wTabela: TClientDataset); virtual;
  end;

var
  frPadraoRelatorioGVGESTOR: TfrPadraoRelatorioGVGESTOR;

implementation

{$R *.dfm}

uses uPrincipal;

{ TfrPadraoRelatorioGVGESTOR }

procedure TfrPadraoRelatorioGVGESTOR.btEditarClick(Sender: TObject);
begin
  // Carrega as consultas SQL e MasterSource, se houver
  pGetConsultaMasterSource;
  pGetConsultaSql;
  // Carrega o arquivo do fastreport correspondente e abre edição
  fCarregaFrxPadrao(Screen.ActiveForm.Name);
  frxReportPadrao.DesignReport;
end;

procedure TfrPadraoRelatorioGVGESTOR.btImprimirClick(Sender: TObject);
begin
  // Prepara o relatório para impressão
  frxReportPadrao.PrepareReport;
  frxReportPadrao.PrintOptions.ShowDialog := False;
  if PrintDialog1.Execute then
     frxReportPadrao.Print;
end;

procedure TfrPadraoRelatorioGVGESTOR.btVisualizarClick(Sender: TObject);
begin
  // Carrega as consultas SQL e MasterSource, se houver
  pGetConsultaMasterSource;
  pGetConsultaSql;
  // Carrega o arquivo do fastreport correspondente e abre a visualização
  fCarregaFrxPadrao(Screen.ActiveForm.Name);
  frxReportPadrao.ShowReport;
end;

procedure TfrPadraoRelatorioGVGESTOR.btSalvarComoClick(Sender: TObject);
begin
  // Recebe o nome do arquivo
  SaveDialog1.FileName := fGetNomeArquivo;
  if SaveDialog1.Execute then
    begin
      // Se o uruário quer visuaizar o arquivo após a exportação
      if MessageDlg('Deseja abrir o arquivo exportado para visualização?', mtInformation, [mbYes,mbNo], 0) = mrYes then
         begin
           frxPDFExport1.OpenAfterExport := True;
           frxDOCXExport1.OpenAfterExport := True;
         end
      else
         begin
           frxPDFExport1.OpenAfterExport := False;
           frxDOCXExport1.OpenAfterExport := False;
         end;
      // Seta nome dos arquivos de acordo com a extensão selecionada
      frxPDFExport1.FileName := StringReplace(SaveDialog1.FileName, ExtractFileExt(SaveDialog1.FileName), EmptyStr, [rfIgnoreCase]) + '.pdf';
      frxDOCXExport1.FileName := StringReplace(SaveDialog1.FileName, ExtractFileExt(SaveDialog1.FileName), EmptyStr, [rfIgnoreCase]) + '.docx';

      frxReportPadrao.PrepareReport;
      // Exporta o arquivo
      case SaveDialog1.FilterIndex of
      1: frxReportPadrao.Export(frxPDFExport1);
      2: frxReportPadrao.Export(frxDOCXExport1);
      end;
    end;
end;

procedure TfrPadraoRelatorioGVGESTOR.fCarregaFrxPadrao(
  wName: String);
begin
  // Carrega o arquivo padrão do fastreport correspondente
  frxReportPadrao.LoadFromFile('C:\TCC - Gestor de Vistorias\Reports\' + wName + '.fr3');
end;

procedure TfrPadraoRelatorioGVGESTOR.FormClose(Sender: TObject;
  var Action: TCloseAction);
begin
  // Fechar tela após fechar o form (MDI)
  Action := caFree;
end;

procedure TfrPadraoRelatorioGVGESTOR.FormCreate(Sender: TObject);
begin
  // Seta a conexão ao arquivo SQL Padrão
  SQLQueryPadrao.SQLConnection := dmConnection.SQLConnectionGVGESTOR;
  frxDBDataset1.DataSet        := SQLQueryPadrao;
  // Verifica perfil do usuário logado e habilita botão de edição do relatório
  if frPrincipal.fGetUsuarioLogado.Perfil = 'Administrador' then
     begin
       btEditar.Enabled := True;
     end;
end;

procedure TfrPadraoRelatorioGVGESTOR.FormKeyDown(Sender: TObject; var Key: Word;
  Shift: TShiftState);
begin
  if Key = VK_ESCAPE then// tecla de atalho para fechar a tela
     Close
  else
  if Key = VK_F4 then // Atalho para visualizar
     btVisualizar.Click
  else
  if Key = VK_F5 then // Atalho para salvar como
     btSalvarComo.Click
  else
  if Key = VK_F6 then  // Atalho para imprimir
     btImprimir.Click
  else
  if Key = VK_F7 then // Atalho para editar
     btEditar.Click;
end;

procedure TfrPadraoRelatorioGVGESTOR.setLimpaFiltros(wTabela: TClientDataset);
begin
  // Limpa filtros da tabela
  wTabela.Filtered := False;
  wTabela.Filter := EmptyStr;
end;

procedure TfrPadraoRelatorioGVGESTOR.setResetMasterSource(wTabela: TClientDataset);
begin
  // Reseta MasterSource da tabela
  wTabela.MasterSource := nil;
  wTabela.IndexFieldNames := EmptyStr;
  wTabela.MasterFields := EmptyStr;
end;

procedure TfrPadraoRelatorioGVGESTOR.FormResize(Sender: TObject);
begin
  // Centralizar form filho dentro do pai
  self.Left := (Application.MainForm.ClientWidth div 2) - (self.Width div 2);
  self.Top := (Application.MainForm.ClientHeight div 2) - (self.Height div 2);
  Application.MainForm.Update;
  self.Update;
end;

end.
